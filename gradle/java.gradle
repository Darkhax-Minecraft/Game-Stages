apply plugin: 'java'

repositories {

    mavenCentral()

    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }

    maven {
        name = 'BlameJared Maven (CrT / Bookshelf)'
        url = 'https://maven.blamejared.com'
    }
}

java {

    // Generate a sources JAR.
    withSourcesJar()
    
    // Generate a JavaDoc JAR.
    withJavadocJar()
    
    // Set minimum language version.
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    it.options.release.set(java_version as int)
}

javadoc {

    // Supress annoying warnings when generating JavaDoc files.
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    manifest {
        attributes([
            'Timestamp': System.currentTimeMillis(),
            'Specification-Title': mod_name,
            'Specification-Vendor': mod_author,
            'Specification-Version': mod_version,
            'Implementation-Title': mod_name,
            'Implementation-Version': mod_version,
            'Implementation-Vendor' : mod_author,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}