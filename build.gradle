plugins {
    id "java"
    alias libs.plugins.vanilla.gradle apply false
    alias libs.plugins.forge.gradle apply false
    alias libs.plugins.mixin apply false
    alias libs.plugins.quilt.loom apply false
}

apply from: 'gradle/property_loader.gradle'
apply from: 'gradle/build_number.gradle'
apply from: 'gradle/git_changelog.gradle'
apply from: 'gradle/version_checker.gradle'

subprojects {
    apply plugin: 'maven-publish'

    apply from: '../gradle/property_loader.gradle'
    apply from: '../gradle/java.gradle'
    apply from: '../gradle/build_number.gradle'
    apply from: '../gradle/git_changelog.gradle'
    apply from: '../gradle/minify_jsons.gradle'
    apply from: '../gradle/signing.gradle'

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {

        enabled = false
    }

    group "${mod_group}.${mod_id}"
    version "${mod_version}+${libs.versions.minecraft.get()}"

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
        it.options.release.set(java_version as int)
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net/' }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Quilt'
            url = 'https://maven.quiltmc.org/repository/release'
        }
        maven {
            name = 'Sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'Parchment'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
    }
}
